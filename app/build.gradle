apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.firebase-crash'
android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"
    def versionPropsFile = file('version.properties')

    if (versionPropsFile.canRead()) {
        def Properties versionProps = new Properties()

        versionProps.load(new FileInputStream(versionPropsFile))

        def code = versionProps['VERSION_CODE'].toInteger() + 1
        versionProps['VERSION_CODE'] = code.toString()
        versionProps.store(versionPropsFile.newWriter(), null)

        defaultConfig {
            applicationId "com.wrewolf.thetaleclient"
            minSdkVersion 19
            targetSdkVersion 27
            versionCode code
            versionName "1.6.9." + code + "_beta"
            resValue "string", "tray__authority", "${applicationId}.tray"
        }
    } else {
        throw new GradleException("Could not read version.properties!")
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            zipAlignEnabled true
        }
        debug {
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            jniDebuggable true
            renderscriptDebuggable true
        }
    }

    dexOptions {
        preDexLibraries true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
        checkAllWarnings true
    }
}


dependencies {
    // latest versions of libraries as of 11.02.2018
    compile 'com.android.support:appcompat-v7:27.0.2'
    compile 'com.android.support:design:27.0.2'
    compile 'com.google.firebase:firebase-core:11.8.0'
    compile 'com.google.firebase:firebase-messaging:11.8.0'
    compile 'com.google.firebase:firebase-crash:11.8.0'
    compile 'com.squareup.okhttp3:okhttp:3.9.1'
    compile 'com.squareup.okhttp3:okhttp-urlconnection:3.9.1'
    compile 'com.squareup.okio:okio:1.13.0'
    compile 'org.greenrobot:eventbus:3.1.1'
    compile 'net.grandcentrix.tray:tray:0.11.1'
    compile 'com.astuetz:pagerslidingtabstrip:1.0.1' // https://github.com/astuetz/PagerSlidingTabStrip
    compile project(':android-support-v4-preferencefragment') // https://github.com/kolavar/android-support-v4-preferencefragment
    implementation 'com.github.chrisbanes:PhotoView:2.0.0' // https://github.com/chrisbanes/PhotoView
    compile 'org.jsoup:jsoup:1.11.2' // https://github.com/jhy/jsoup


    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    implementation project(':api')
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.9'
    implementation 'com.jakewharton.rxrelay2:rxrelay:2.0.0'
    compile 'com.jakewharton.rxbinding2:rxbinding-kotlin:2.1.1'

    implementation 'com.google.dagger:dagger-android:2.14.1'
    implementation 'com.google.dagger:dagger-android-support:2.14.1'
    kapt 'com.google.dagger:dagger-android-processor:2.14.1'
    kapt "com.google.dagger:dagger-compiler:2.14.1"

}

android.applicationVariants.all { variant ->
    variant.getOutputs().all { output ->
        if (outputFileName != null && output.outputFileName.endsWith('.apk')) {
            outputFileName = new File(output.outputFile.parent,
                    variant.buildType.name + "-" + variant.mergedFlavor.versionName + "-b" + variant.mergedFlavor.versionCode + ".apk")
        }
    }
}

apply plugin: 'com.google.gms.google-services'
repositories {
    mavenCentral()
}